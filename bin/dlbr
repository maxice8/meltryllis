#!/usr/bin/ion
# SPDX-License-Identifier: GPL-3.0-only
# dlbr BRANCH... - Delete local and remote branches matching name
fn delete_remote_branch branch:str
	if not git push --quiet origin --delete $branch 2>/dev/null
		if $? -ne 1
			printerr "Failed to delete remote '$branch'"
			return 0
		end
	else
		printok "Deleted remote '$branch'"
	end
end

fn delete_local_branch branch:str
	if not git rev-parse --verify "$branch" &>/dev/null
		return 0
	end

	if test $(git-current-branch) = $branch
		export PRINTOK_QUIET=yes
		gbr master
	end

	if not git branch -D "$branch" &>/dev/null
		printerr "Failed to delete local '$branch'"
		return 1
	else
		printok "Deleted local '$branch'"
	end
end

test $len(@args) -eq 1 && exit 1
for branch in @args[1..]
	not exists -s DLBR_LOCAL && delete_remote_branch $branch
	delete_local_branch $branch
end
