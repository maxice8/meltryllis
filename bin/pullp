#!/usr/bin/ion
let _REALBASE = $(tracking-branch)

let remote = $(guess-remote)
if not exists -s remote
	exit 1
end

let pushmode = $(git config --get push.default)

let branch = ""
if test "current" = $pushmode
	let prefix = $(alpine-stable-prefix $(git-current-branch))
	if exists -s prefix
		let branch = $prefix-stable
	else
		let branch = master
	end
else
	let branch = $(git-current-branch)
end

let url = $(git config --local remote.$remote.url)

if exists -s PULLP_DRY_RUN
	echo Args: @args
	echo Remote Branch: $_REALBASE
	echo Remote: $remote
	echo Local Branch: $branch
	echo Url: $url
	echo FinalCommand: git pull --quiet $remote --rebase $_REALBASE
	exit 0
end

git pull --quiet $remote --rebase $_REALBASE && \
	printok "Rebased '$branch' on '$remote/$_REALBASE' from '$url'" || \
	printerr "Failed to rebase '$branch' on '$remote/$_REALBASE' from '$url'"
