#!/usr/bin/ion
if test $len(@args) -eq 1
	echo unpk '<archives>'
	exit 0
end

for pk in @args[1..]
	match $pk
		case [*.tar.bz2 *.tar.gz *.tar.xz *.tbz2 *.tgz *.txz *.tar *.xbps *.apk]
			tar xfv $pk
		case *.lzma
			unlzma -cd "$pk"
		case *.bz2
			bunzip2 -cd "$pk"
		case *.rar
			unrar x -ad "$pk"
		case [*.gz *.zst]
			unzstd -cq "$pk"
		case *.zip
			unzip "$pk"
		case *.Z
			uncompress -c "$pk"
		case [*.7z *.arj *.cab *.chm *.dmg *.iso *.lzh *.msi *.rpm *.udf *.wim *.xa]
			7z x "$pk"
		case *.xz
			unxz -cd "$pk"
		case _; echo file format for "$pk" not supported
	end
end
