#!/usr/bin/ion

fn usage
	echo "wait-path [type] <path>

types:
	- p|namedpipe   : named pipes
	- s|socket	: sockets
	- f|file	: regular files
	- d|directory   : directories
	- l|symlink	: symbolic links
	- e|executable  : executable files"
exit 1
end

test $len(@args) -eq 1 && usage

if test $len(@args) -eq 2
	let args = [@args[0] -e @args[1]]
end

let type = ""
match @args[1]
	case [p namedpipe]
		let type = -p
	case [s socket]
		let type = -S
	case [f file]
		let type = -f
	case [d directory]
		let type = -d
	case [l symlink]
		let type = -l
	case [e executable]
		let type = -x
	case -e
		let type = -e
	case _; exit 1
end

while true
	if test $type @args[2]
		exit
	end
	inotifywait -qq -e create -e moved_to -e modify -e attrib $parent(@args[2])
end
